{
	/// Find Numbers for Page
	{
		// String with multiple lines

		Breaking Reflection
		Separation Distillation
		
		// [Single Line Strings]

		/// Find Number for Line
		{
			// Single Line String
			
			Blank Reflection
			Separation Distillation
			
			// [Single Char Strings]

			Vacant Reflection
			Huginn's Gambit

			/// Add Digit to List
			{
				// Single Char
				Gemini Decomposition
				Single's Purification
				Jester's Gambit
				Input Purification

				// Num/Null
				// [Single Char]

				Nullary Reflection
				Inequality Distillation
				Jester's Gambit
				Vacant Reflection

				// []
				// [Single Char]
				// False/True

				Augur's Exaltation
				// [Single Digit]/[]

				Muninn's Reflection
				// [Single Digits]
				// [Single Digit]/[]
				Combination Distillation
				Huginn's Gambit
			}

			Jester's Gambit
			Thoth's Gambit
			Bookkeeper's Gambit: v
			
			Muninn's Reflection
			Gemini Decomposition
			Retrograde Purification
			Speaker's Decomposition
			Bookkeeper's Gambit: v-
			Jester's Gambit
			
			Speaker's Decomposition
			Bookkeeper's Gambit: v-
			Concatenation Distillation
			Input Purification
			
			// Number
		}
		
		Jester's Gambit
		Thoth's Gambit

		Flock's Disintegration
		// Number
		// Number
		// ...		
	}

	/// Outermost
	Mind's Reflection
	Compass' Purification
	Mind's Reflection
	Alidade's Purification
	Archer's Distillation
	Reader's Purification
	
	Thoth's Gambit
	// [numbers for each line]

	Running Sum Purification
	Retrograde Purification
	Speaker's Decomposition
	Bookkeeper's Gambit: v-
	Reveal
}